plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT' apply false
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

if (rootProject == project){
	apply plugin: "fabric-loom";
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "Forge"
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		name = "Minecraft"
		url = "https://libraries.minecraft.net/"
	}
	mavenLocal()
	mavenCentral()
}

configurations {
	shade
}

loom {
	mods {
		feltfabric {
			sourceSet sourceSets.main
			configuration configurations.shade
		}
	}
}

dependencies {
	implementation 'org.jetbrains:annotations:20.1.0'

	// To change the versions see the gradle.properties file
	if (rootProject == project){
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		// Fabric API. This is technically optional, but you probably want it anyway.
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	}

	compileOnly('cpw.mods:modlauncher:10.0.+')
	implementation('org.ow2.asm:asm:9.2')
	implementation('org.ow2.asm:asm-commons:9.2')
	implementation('org.ow2.asm:asm-tree:9.2')
	implementation('org.apache.logging.log4j:log4j-api:2.17.0')
	implementation('org.apache.maven:maven-artifact:3.8.1')
	compileOnly('cpw.mods:securejarhandler:0.9.52')
	testCompileOnly('org.junit.jupiter:junit-jupiter-engine:5.8.+')
	//shade implementation("net.jodah:typetools:0.8.3")
	//shade implementation("org.apache.maven:maven-artifact:${APACHE_MAVEN_ARTIFACT_VERSION}")
	//shade implementation("com.moandjiezana.toml:toml4j:0.7.2")
	//shade implementation("com.googlecode.json-simple:json-simple:1.1.1")

	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}


shadowJar {
	dependsOn(jar)
	configurations = [project.configurations.shade]
	archiveClassifier.set('')
}

build.dependsOn(shadowJar)

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
